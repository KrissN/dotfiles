#!/bin/zsh

__RESET_SCREEN_TITLE=
__EXPLICIT_SCREEN_TITLE=

screen_title() {
    __EXPLICIT_SCREEN_TITLE=$1
    __set_screen_title $1
    __RESET_SCREEN_TITLE=
}

__set_screen_title() {
    # set screen title
    if [[ -n $1 ]]; then
        print -Pn "\ek$1\e\\"
    fi
}

set_screen_hostname() {
    # set screen title to hostname
    __set_screen_title $__CURRENT_HOSTNAME
}

set_screen_screen_session() {
    # set screen title to the name of the screen sub-session
    set -- $(echo $1 | sed 's/.*\sscreen\s\s*//;')
    zparseopts -E -- S:=session r:=session d=d
    __set_screen_title $(echo $session | sed -e 's/.*\s\s*\(\d+\.\)*//')
}

screen() {
    # some commands run within screen change the value of $COLUMNS, so
    # we save that variable and then restore it
    oldcols=$COLUMNS
    /usr/bin/screen $@
    export COLUMNS=$oldcols
}

zsh_preexec_update_screen_title() {
    if [[ -n $__EXPLICIT_SCREEN_TITLE ]]; then
        __set_screen_title $__EXPLICIT_SCREEN_TITLE
    else
        case "$1" in
            *ssh*)
                set_screen_hostname
                __RESET_SCREEN_TITLE=1
                ;;
            *screen*)
                set_screen_screen_session "$1"
                __RESET_SCREEN_TITLE=1
                ;;
        esac
    fi
}

zsh_precmd_update_screen_title() {
    if [[ $__RESET_SCREEN_TITLE == 1 ]]; then
        set_screen_hostname
        __RESET_SCREEN_TITLE=
    fi
}

if [[ $TERM[0,6] == "screen" ]]; then
    set_screen_hostname
    preexec_functions[$(($#preexec_functions + 1))]='zsh_preexec_update_screen_title'
    precmd_functions[$(($#precmd_functions + 1))]='zsh_precmd_update_screen_title'

    if [[ $(uname -s) == 'AIX' ]]; then
        # "screen" is not a sane term type on AIX
        export TERM=xterm
    fi
fi
