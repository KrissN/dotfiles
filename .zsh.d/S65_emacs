#!/bin/zsh

# set this to 1 to use one emacs server per virtualenv.  anything else
# uses a single emacs server for everything, and delegates venv
# management to emacs itself.
EMACS_SERVER_PER_VIRTUALENV=0

function _emacs_server_name() {
    if [[ $EMACS_SERVER_PER_VIRTUALENV == 1 && -n $VIRTUAL_ENV ]]; then
        basename "$VIRTUAL_ENV"
    else
        echo server
    fi
}

function __start_emacs_server() {
    # start emacs server
    name=$(_emacs_server_name)
    for pid in $(pgrep -U "$USER" -f "$__emacs_bin"); do
        if lsof -F n -a -U -p "$pid" | grep -q "^n.*/$name"; then
            # already running
            return
        fi
    done
    $__emacs_bin --daemon="$name"
}

if [[ -e /Applications/Emacs.app/Contents/MacOS/bin ]]; then
    # this needs to be first in PATH, or at least before /usr/bin.
    # The easiest way to do that is to remove it before adding it.
    pathremove /Applications/Emacs.app/Contents/MacOS/bin
    pathmunge /Applications/Emacs.app/Contents/MacOS/bin
fi

if type emacs >& /dev/null; then
    if [[ -z $__emacs_bin ]]; then
        if [[ $(type emacs) == "emacs is a shell function" ]]; then
            unfunction emacs
        fi

        # OS X crap
        if [[ -e /Applications/Emacs.app/Contents/MacOS/Emacs ]]; then
            __emacs_bin="/Applications/Emacs.app/Contents/MacOS/Emacs"
        else
            __emacs_bin=$(which emacs)
        fi
    fi
    __emacs_version=$($__emacs_bin --version | head -n 1 | \
        awk '{ split($NF, v, "."); print v[1] "." v[2]; }')

    if type emacsclient >& /dev/null && [[ $__emacs_version -ge 23.1 ]]; then
        function emacs() {
            __start_emacs_server
            args="-n -s $(_emacs_server_name)"
            if [[ $(uname) == "Darwin" || -n $DISPLAY ]]; then
                args="$args -c"
            else
                args="$args -t"
            fi
            args="$args $@"
            emacsclient ${=args}
        }

        function emacs-kill() {
            emacsclient -e -s "$(_emacs_server_name)" '(kill-emacs)'
        }

        export ALTERNATIVE_EDITOR=""
    fi
    export EDITOR="emacs -nw"
    alias emcas=emacs
elif type vim >& /dev/null; then
    export EDITOR=vim
    alias emcas=$EDITOR
else
    export EDITOR=vi
    alias emcas=$EDITOR
fi

alias vi=$EDITOR

# don't use python inside a virtualenv to run pymacs
export PYMACS_PYTHON=$(which python)
